---
title:  "Lighting"

categories:
  -  OpenGL
tags:
  - [Graphics]

toc: true
toc_sticky: true

date: 2024-07-22
last_modified_at: 2024-07-22
---


**Main Reference <br>- [Learn OpenGL](https://learnopengl.com/) <br>- [Rinthel Kwon - OpenGL Lecture](https://www.youtube.com/watch?v=kEAKvJKnvfA&list=PLvNHCGtd4kh_cYLKMP_E-jwF3YKpDP4hf&index=1)**
{: .notice--warning}

<br>

# Phong Shading

![09_phong_illumination_model](https://github.com/user-attachments/assets/5722e56e-767f-4092-919a-b74c5d6b402a)

Phong Shading은 가장 대표적인 Local Illumination 모델이다. 즉 다른 물체에 반사되어 발생한 빛(반사광)을 고려하지 않는다는 의미이다. 게임과 같은 Real Time Rendering에서는 많이 쓰이는 기법이다. 우선 간단하게만 설명하자면 Phong Shading에서 물체의 색은 Ambient(고유 색) + Diffuse(난반사) + Specular(정반사)에 의해 결정된다.

<br>

# Ambient Light

| No Ambient | Add Ambient |
|:-:|:-:|
|![2](https://github.com/inhopp/inhopp/assets/96368476/4b5d6cc1-c1f8-4292-87f2-751f070c8972)|![3](https://github.com/inhopp/inhopp/assets/96368476/196020e9-972f-4bd5-b184-1a0a9d5c74b4)| 

Ambient Color란 물체가 빛을 받지 않아도 갖고 있는 물체의 고유한 색이다. 현실 세계에서는 약간 이상하게 들리지만 렌더링 과정에서는 필수적인 요소이다. 예를 들어 위 사진의 왼쪽 그림은 ambient color가 없는 경우의 렌더링 결과이다. 자원의 한계 상 모든 반사광을 계산할 수 없기 때문에 빛을 받지 못하는 오브젝트들이 존재하게 된다. 이런 경우 모든 물체가 어느정도의 반사광을 받는다고 가정한 뒤 고유 색(ambient color)를 갖게 해주면 훨씬 자연스러운 결과가 나오게 된다. 따라서 Ambient Color는 빛의 방향, normal의 방향, 시선(카메라) 방향과 상관 없이 상수값으로 처리된다.

<br>

``` glsl
// fragment shader
void main() {
  vec3 ambient = ambientStrength * lightColor;
  vec3 result = ambient * objectColor;
  fragColor = vec4(result, 1.0);
}
```

- abient color의 결과값은 광원의 색(light color) x 물체 고유의 색(ambient color) x 밝기 (abient strength)로 결정
- ambient color만으로는 물체에 입체감이 느껴지지 않음

<br>

![233115](https://github.com/user-attachments/assets/96a669d5-5d2e-4a4b-ba67-6f6841ec4e29)

<br>

## Diffuse  Light

| Diffuse | Formula |
|:-:|:-:|
|![234343](https://github.com/user-attachments/assets/66246e05-ed33-4fb1-8132-44f31168dba1)|![5](https://github.com/inhopp/inhopp/assets/96368476/7e481130-c721-43be-a9fd-98f9b75d0f94)| 

- 빛이 물체 표면에 부딪혔을 때 모든 방향으로 고르게 퍼지는 빛
- 시선 방향과 상관 없이 빛의 방향, 물체 표면의 방향에 의해 결정
- 빛이 normal과 방향이 가까울 수록 diffuse가 강해짐
- 90도가 넘어가면(내적값이 음수) 빛을 받지 않는다고 판단

<br>

``` c++
float vertices[] = { // pos.xyz, normal.xyz, texcoord.uv
-0.5f, -0.5f, -0.5f,  0.0f,  0.0f, -1.0f, 0.0f, 0.0f,
0.5f, -0.5f, -0.5f,  0.0f,  0.0f, -1.0f, 1.0f, 0.0f,
0.5f,  0.5f, -0.5f,  0.0f,  0.0f, -1.0f, 1.0f, 1.0f,
-0.5f,  0.5f, -0.5f,  0.0f,  0.0f, -1.0f, 0.0f, 1.0f,

-0.5f, -0.5f,  0.5f,  0.0f,  0.0f,  1.0f, 0.0f, 0.0f,
0.5f, -0.5f,  0.5f,  0.0f,  0.0f,  1.0f, 1.0f, 0.0f,
0.5f,  0.5f,  0.5f,  0.0f,  0.0f,  1.0f, 1.0f, 1.0f,
-0.5f,  0.5f,  0.5f,  0.0f,  0.0f,  1.0f, 0.0f, 1.0f,

-0.5f,  0.5f,  0.5f, -1.0f,  0.0f,  0.0f, 1.0f, 0.0f,
-0.5f,  0.5f, -0.5f, -1.0f,  0.0f,  0.0f, 1.0f, 1.0f,
-0.5f, -0.5f, -0.5f, -1.0f,  0.0f,  0.0f, 0.0f, 1.0f,
-0.5f, -0.5f,  0.5f, -1.0f,  0.0f,  0.0f, 0.0f, 0.0f,

0.5f,  0.5f,  0.5f,  1.0f,  0.0f,  0.0f, 1.0f, 0.0f,
0.5f,  0.5f, -0.5f,  1.0f,  0.0f,  0.0f, 1.0f, 1.0f,
0.5f, -0.5f, -0.5f,  1.0f,  0.0f,  0.0f, 0.0f, 1.0f,
0.5f, -0.5f,  0.5f,  1.0f,  0.0f,  0.0f, 0.0f, 0.0f,

-0.5f, -0.5f, -0.5f,  0.0f, -1.0f,  0.0f, 0.0f, 1.0f,
0.5f, -0.5f, -0.5f,  0.0f, -1.0f,  0.0f, 1.0f, 1.0f,
0.5f, -0.5f,  0.5f,  0.0f, -1.0f,  0.0f, 1.0f, 0.0f,
-0.5f, -0.5f,  0.5f,  0.0f, -1.0f,  0.0f, 0.0f, 0.0f,

-0.5f,  0.5f, -0.5f,  0.0f,  1.0f,  0.0f, 0.0f, 1.0f,
0.5f,  0.5f, -0.5f,  0.0f,  1.0f,  0.0f, 1.0f, 1.0f,
0.5f,  0.5f,  0.5f,  0.0f,  1.0f,  0.0f, 1.0f, 0.0f,
-0.5f,  0.5f,  0.5f,  0.0f,  1.0f,  0.0f, 0.0f, 0.0f,
};

m_vertexLayout->SetAttrib(0, 3, GL_FLOAT, GL_FALSE, sizeof(float) * 8, 0);
m_vertexLayout->SetAttrib(1, 3, GL_FLOAT, GL_FALSE, sizeof(float) * 8, sizeof(float) * 3);
m_vertexLayout->SetAttrib(2, 2, GL_FLOAT, GL_FALSE, sizeof(float) * 8, sizeof(float) * 6);
```

- Diffuse부터는 color를 계산하는데 normal 벡터가 필요해짐
- normal VBO 추가 & VAO 수정
- 이때 fragment shader에서 사용하는 normal은 local-normal이 아니라, world 좌표계에서의 normal 벡터

<br>

``` glsl
// vertex shader
#version 330 core

layout (location = 0) in vec3 aPos;
layout (location = 1) in vec3 aNormal;
layout (location = 2) in vec2 aTexCoord;

uniform mat4 transform;
uniform mat4 modelTransform;

out vec3 normal;
out vec2 texCoord;
out vec3 position;

void main() {
    gl_Position = transform * vec4(aPos, 1.0);
    normal = (transpose(inverse(modelTransform)) * vec4(aNormal, 0.0)).xyz;
    texCoord = aTexCoord;
    position = (modelTransform * vec4(aPos, 1.0)).xyz;
}
```

- 이제 fragment shader에 넘겨줘야 할 값은 vertex의 canonical 좌표(gl_position) 뿐만이 아니라 여러가지로 늘어나기 시작
- 우선 world 좌표계에서의 normal 값을 넘겨줘야 함
    - transpose-inverse(model)을 곱해주는 이유는 단순한 선형 변환시 수직 관계가 보존되지 않음
    - 아래 그림에서 자세히 설명..
- 마찬가지로 vertex의 world position 넘겨줘야 함

<br>

![Ect-48](https://github.com/inhopp/inhopp/assets/96368476/2a4a7d83-5c09-4804-9c19-fa8e4b291a05)

<br>

``` glsl
// fragment shader
void main() {
    vec3 ambient = ambientStrength * lightColor;
    vec3 lightDir = normalize(lightPos - position);
    vec3 pixelNorm = normalize(normal);
    vec3 diffuse = max(dot(pixelNorm, lightDir), 0.0) * lightColor;
    vec3 result = (ambient + diffuse) * objectColor;
    fragColor = vec4(result, 1.0);
}
```

<br>

![003315](https://github.com/user-attachments/assets/6d2eaacf-a4e0-4398-9cca-afaf2ebd6632)

<br>



## Specular Light

| Specular | Formula |
|:-:|:-:|
|![234343](https://github.com/user-attachments/assets/66246e05-ed33-4fb1-8132-44f31168dba1)|![7](https://github.com/inhopp/inhopp/assets/96368476/c101c0eb-73b4-43a0-afb2-ed1e73621f4d)|

- 빛이 물체 표면에 부딪혀 정반사되는 빛
- 시선 방향이 반사광의 방향과 동일할 때 가장 강한 빛을 받음
- 정반사되는 특정 위치를 반짝반짝하게 해줌
- 보통 specular값을 n제곱 해서 사용 (n : shineness)
- SpecularStrengh는 재질마다 다름
  - 일반적인 오브젝트는 여러 재질로 이루어져 있는데, Strengh값을 어떻게 조절..?
  - Specular Strengh를 나타내는 Texture Map 이용

<br>

``` glsl
// fragment shader
void main() {
    vec3 ambient = ambientStrength * lightColor;
    vec3 lightDir = normalize(lightPos - position);
    vec3 pixelNorm = normalize(normal);
    vec3 diffuse = max(dot(pixelNorm, lightDir), 0.0) * lightColor;

    vec3 viewDir = normalize(viewPos - position);
    vec3 reflectDir = reflect(-lightDir, pixelNorm);
    float spec = pow(max(dot(viewDir, reflectDir), 0.0), specularShininess);
    vec3 specular = specularStrength * spec * lightColor;
 
    vec3 result = (ambient + diffuse + specular) * objectColor;
    fragColor = vec4(result, 1.0);
}
```

<br>

![004650](https://github.com/user-attachments/assets/7b97c05b-fc2a-49b3-84cb-927ac1602f06)



<br>

# Result

| Ambient | Ambient + Diffuse | Ambient + Diffuse + Specular |
|:-:|:-:|:-:|
| ![233115](https://github.com/user-attachments/assets/96a669d5-5d2e-4a4b-ba67-6f6841ec4e29) | ![003315](https://github.com/user-attachments/assets/6d2eaacf-a4e0-4398-9cca-afaf2ebd6632) | ![004650](https://github.com/user-attachments/assets/7b97c05b-fc2a-49b3-84cb-927ac1602f06) |



<br>
<br>


[맨 위로 이동하기](#){: .btn .btn--primary }{: .align-right}